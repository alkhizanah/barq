type Token = struct {
    tag Token::Tag,
    range Token::Range,
};

type Token::Tag = enum {
    eof,
    invalid,
    identifier,
    int,
    float,
    string_literal,
    forward_slash,
    equal_sign,
    double_equal_sign,
    semicolon,
};

fn Token::Tag::to_string(self Token::Tag) [*]const u8 {
    if self == Token::Tag::eof {
        return "eof";
    } else if self == Token::Tag::identifier {
        return "identifier";
    } else if self == Token::Tag::int {
        return "int";
    } else if self == Token::Tag::float {
        return "float";
    } else if self == Token::Tag::string_literal {
        return "string_literal";
    } else if self == Token::Tag::forward_slash {
        return "forward_slash";
    } else if self == Token::Tag::equal_sign {
        return "equal_sign";
    } else if self == Token::Tag::double_equal_sign {
        return "double_equal_sign";
    } else if self == Token::Tag::semicolon {
        return "semicolon";
    }

    return "invalid";
}

type Token::Range = struct {
    start usize,
    end usize,
};

type Lexer = struct {
    index usize,
    buffer [*]const u8,
};

type Lexer::State = enum {
    start,
    identifier,
    number,
    string_literal,
    string_literal_back_slash,
    forward_slash,
    comment,
    equal_sign,
};

fn Lexer::is_whitespace(ch u8) bool {
    return (ch == ' ') | (ch == '\r') | (ch == '\n') | (ch == '\t');
}

fn Lexer::is_identifier(ch u8) bool {
    return (!(ch < 'a') & !(ch > 'z')) | (!(ch < 'A') & !(ch > 'Z')) | (ch == '_');
}

fn Lexer::is_number(ch u8) bool {
    return !(ch < '0') & !(ch > '9');
}

fn Lexer::is_identifier_continue(ch u8) bool {
    return Lexer::is_identifier(ch) | Lexer::is_number(ch) | (ch == ':');
}

fn Lexer::next(self *Lexer) Token {
    var result Token;

    result.tag = Token::Tag::eof;
    result.range.start = self.index;
    result.range.end = self.index;

    var state Lexer::State = Lexer::State::start;

    while true {
        var ch = self.buffer[self.index];

        if state == Lexer::State::start {            
            if Lexer::is_whitespace(ch) {
            } else if Lexer::is_identifier(ch) {
                result.tag = Token::Tag::identifier;
                result.range.start = self.index;
                
                state = Lexer::State::identifier;
            } else if Lexer::is_number(ch) {
                result.tag = Token::Tag::int;
                result.range.start = self.index;
                
                state = Lexer::State::number;
            } else if ch == 0 {
                result.tag = Token::Tag::eof;
                result.range.start = self.index;
                result.range.end = self.index;

                break;
            } else if ch == ';' {
                result.tag = Token::Tag::semicolon;
                result.range.start = self.index;
                self.index = self.index + 1;
                result.range.end = self.index;

                break;
            } else if ch == '"' {
                result.tag = Token::Tag::string_literal;
                result.range.start = self.index;
                
                state = Lexer::State::string_literal;
            } else if ch == '/' {
                result.tag = Token::Tag::forward_slash;
                result.range.start = self.index;
                
                state = Lexer::State::forward_slash;
            } else if ch == '=' {
                result.tag = Token::Tag::equal_sign;
                result.range.start = self.index;
                
                state = Lexer::State::equal_sign;
            } else {
                result.tag = Token::Tag::invalid;
                result.range.start = self.index;
                self.index = self.index + 1;
                result.range.end = self.index;

                break;
            }
        } else if state == Lexer::State::identifier {
            if !Lexer::is_identifier_continue(ch) {
                result.range.end = self.index;

                break;
            }
        } else if state == Lexer::State::number {
            if ch == '.' {
                result.tag = Token::Tag::float;
            } else if !Lexer::is_number(ch) {
                result.range.end = self.index;

                break;
            }
        } else if state == Lexer::State::string_literal {
            if (ch == 0) | (ch == '\n') {
                result.tag = Token::Tag::invalid;
                result.range.start = self.index;
                self.index = self.index + 1;
                result.range.end = self.index;

                break;
            } else if ch == '\\' {
                state = Lexer::State::string_literal_back_slash;
            } else if ch == '"' {
                result.range.end = self.index;
                self.index = self.index + 1;

                break;
            }
        } else if state == Lexer::State::string_literal_back_slash {
            if (ch == 0) | (ch == '\n') {
                result.tag = Token::Tag::invalid;
                result.range.start = self.index;
                self.index = self.index + 1;
                result.range.end = self.index;

                break;
            } else {
                state = Lexer::State::string_literal;
            }
        } else if state == Lexer::State::forward_slash {
            if ch == '/' {
                state = Lexer::State::comment;
            } else {
                result.range.end = self.index;

                break;
            }
        } else if state == Lexer::State::comment {
            if ch == 0 {
                result.tag = Token::Tag::eof;
                result.range.start = self.index;
                result.range.end = self.index;

                break;
            }

            if ch == '\n' {
                state = Lexer::State::start;
            }
        } else if state == Lexer::State::equal_sign {
            if ch == '=' {
                result.tag = Token::Tag::double_equal_sign;
                self.index = self.index + 1;
                result.range.end = self.index;

                break;
            } else {
                result.range.end = self.index;

                break;
            }
        }

        self.index = self.index + 1;
    }

    return result;
}

// Debugging the Lexer, we still don't have a module system to refactor this stuff into main.cerm

fn print(str [*]const u8) {
    var len usize = 0;

    // This should be replaced with a simple str.len if we implement slices, as it is not a good idea to use a loop to get the length of a string
    while str[len] != 0 {
        len = len + 1;
    }

    // This should be refactored into the Standard Library as a write into the stdout file descriptor (or "handle" in Windows)
    asm {
        "syscall"
        :
        : "{rax}" (1 as usize),
          "{rdi}" (1 as usize),
          "{rsi}" (str as usize),
          "{rdx}" (len),
        : "rcx", "r11", "memory"
    };
}

fn main() {
    var lexer Lexer;

    lexer.index = 0;
    lexer.buffer = "var Hello::World = \"\\\"\";";

    print("buffer: ");
    print(lexer.buffer);
    print("\n");

    while true {
        var token = Lexer::next(&lexer);

        print("token tag: ");
        print(Token::Tag::to_string(token.tag));
        print("\n");

        if token.tag == Token::Tag::eof {
            break;
        }
    }
}
